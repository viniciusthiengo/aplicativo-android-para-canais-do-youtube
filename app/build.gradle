buildscript {
    repositories {
        maven { url 'https://plugins.gradle.org/m2/'}
    }
    dependencies {
        classpath 'gradle.plugin.com.onesignal:onesignal-gradle-plugin:[0.12.8, 0.99.99]'
    }
}
apply plugin: 'com.onesignal.androidsdk.onesignal-gradle-plugin'

repositories {
    maven { url 'https://maven.google.com' }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.0"

    defaultConfig {
        applicationId "thiengo.com.br.canalvinciusthiengo"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        manifestPlaceholders = [
            onesignal_app_id: 'e4c1751c-0ce9-40f7-8590-f9daa7927539',
            onesignal_google_project_number: 'REMOTE'
        ]
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'

    /**
     * Para RecyclerView e outros.
     */
    implementation 'com.google.android.material:material:1.2.0'

    /*
     * Retrofit API para comunicação remota. O Parse é
     * realizado com a Gson API.
     * */
    def retrofit_version = '2.9.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    /*
     * Picasso API para carregamento de imagens remotas
     * de maneira eficiente.
     * */
    implementation 'com.squareup.picasso:picasso:2.71828'

    /*
     * Room API para persistência local eficiente utilizando
     * "por debaixo dos panos" a persistência SQLite.
     * */
    def room_version = '2.2.5'
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    /*
     * Para uso da WorkManager API. API responsável por
     * trabalhos em background quando o app não está em uso.
     * */
    implementation "androidx.work:work-runtime-ktx:2.4.0"

    /*
     * Para uso da OneSignal API. API redundante de
     * notificações no projeto.
     * */
    implementation 'com.onesignal:OneSignal:[3.15.1, 3.99.99]'
    implementation 'com.google.firebase:firebase-messaging:20.2.4'
}